extends Node
# Utility functions.


const RAY_LENGTH: float = 1000


# Check mouse position and warp mouse to opposite side of screen when going off screen edge.
func check_mouse_margins(view: Viewport, current_mouse_pos: Vector2, margin: float) -> void:
	var viewport_visible_rectangle: Rect2i = Rect2i(view.get_visible_rect())
	var viewport_size: Vector2i = viewport_visible_rectangle.size
	
	var new_mouse_pos: Vector2 = Vector2.ZERO
	new_mouse_pos.y = current_mouse_pos.y
	
	# Check if mouse x position is on edge of screen.
	if current_mouse_pos.x < margin:
		new_mouse_pos.x = viewport_size.x - margin
	elif current_mouse_pos.x > viewport_size.x - margin:
		new_mouse_pos.x = margin
	else:
		new_mouse_pos.x = current_mouse_pos.x
	
	# Check if mouse y position is on edge of screen.
	if current_mouse_pos.y < margin:
		new_mouse_pos.y = viewport_size.y - margin
	elif current_mouse_pos.y > viewport_size.y - margin:
		new_mouse_pos.y = margin
	else:
		new_mouse_pos.y = current_mouse_pos.y
	
	
	if current_mouse_pos == new_mouse_pos: return
	
	# If mouse is going off screen then warp mouse to opposite side.
	else: Input.warp_mouse(new_mouse_pos)


# Clamps current yaw (y) rotation to a central yaw rotation +/- a given angle.
func clamp_yaw(current_yaw: float, center_yaw: float, angle: float) -> float:
	var relative_angle := wrapf(current_yaw - center_yaw, -PI, PI)
	relative_angle = clampf(relative_angle, -angle, angle)
	return center_yaw + relative_angle


func get_yaw(node: Node3D) -> float:
	return atan2(-node.global_transform.basis.z.x, -node.global_transform.basis.z.z)


# Return dictionary of raycast intersection from one Vector3 to another.
func raycast(camera: Camera3D, from: Vector3, to: Vector3, mask: int = 1) -> Dictionary:
	var ray_query: PhysicsRayQueryParameters3D = PhysicsRayQueryParameters3D.create(from, to)
	ray_query.collision_mask = mask
	
	var space_state: PhysicsDirectSpaceState3D = camera.get_world_3d().get_direct_space_state()
	var result: Dictionary = space_state.intersect_ray(ray_query)
	if result: return result
	else: return {}


# Return 3d position of mouse.
func raycast_3d_position_from_mouse(camera: Camera3D, mouse_pos: Vector2, mask: int = 1) -> Vector3:
	var result: Dictionary = raycast_from_mouse(camera, mouse_pos, mask)
	
	if result.has("position"): return result["position"]
	else: return Vector3.ZERO


# Return collision from raycast.
func raycast_collider_from_mouse(camera: Camera3D, mouse_pos: Vector2, mask: int = 1) -> Object:
	var result: Dictionary = raycast_from_mouse(camera, mouse_pos, mask)
	
	if result.has("collider"): return result["collider"]
	else: return null


# Return dictionary of raycast intersection from mouse position.
func raycast_from_mouse(camera: Camera3D, mouse_pos: Vector2, mask: int = 1) -> Dictionary:
	var from: Vector3 = camera.project_ray_origin(mouse_pos)
	var to: Vector3 = from + camera.project_ray_normal(mouse_pos) * RAY_LENGTH
	return raycast(camera, from, to, mask)


# Return yaw (y) rotation for a given Node3D to rotate towards given direction.
func yaw_towards_direction(node: Node3D, direction: Vector3) -> float:
	var pos_2D: Vector2 = Vector2(-node.transform.basis.z.x, -node.transform.basis.z.z)
	var goal_2D: Vector2 = Vector2(direction.x, direction.z)
	return pos_2D.angle_to(goal_2D)
